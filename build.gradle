buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath "gradle.plugin.com.modrinth.minotaur:Minotaur:1.1.0"
    }
}

plugins {
    id "architectury-plugin" version "3.1-SNAPSHOT"
//    id "forgified-fabric-loom" version "0.7.0-SNAPSHOT" apply false
    id "forgified-fabric-loom" version "0.7.0.100" apply false
    id "io.freefair.lombok" version "5.3.3.3" apply false
    id "com.modrinth.minotaur" version "1.1.0"
    id "com.matthewprenger.cursegradle" version "1.4.0" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "forgified-fabric-loom"
    apply plugin: "io.freefair.lombok"

    generateLombokConfig.enabled = false

    loom {
        silentMojangMappingsLicense()
        useFabricMixin = true
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
//        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
        maven { url "https://maven.architectury.dev/" }
        maven { url 'https://maven.blamejared.com' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://maven.terraformersmc.com/releases/' }
//        maven { url 'https://repo.repsy.io/mvn/howboutno/main' }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"

        // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
        // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
        // We'll use that if it's available, but otherwise we'll use the older option.
        def targetVersion = 8
        if (JavaVersion.current().isJava9Compatible()) {
            options.release = targetVersion
        }
    }

    java {
        withSourcesJar()
    }
}

if (file("key.properties").exists()) {
    task publishAll {
//        dependsOn = [':forge:curseforge', ':fabric:curseforge', ':forge:modrinthForge', ':fabric:modrinthFabric']
        dependsOn = [':forge:curseforge', ':fabric:curseforge']
    }
}