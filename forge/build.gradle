plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

configurations {
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    mixinConfigs = ["outvoted.mixins.json", "outvoted-forge.mixins.json"]
    useFabricMixin = true

    dataGen {
        mod "outvoted"
    }

    launches {
        data {
            arg "--existing", file("../common/src/main/resources").absolutePath
        }
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "me.shedaniel:architectury-forge:${rootProject.architectury_version}"

    modImplementation "software.bernie.geckolib:geckolib-forge-1.16.5:${rootProject.geckolib_version}"
    modCompile("me.shedaniel.cloth:cloth-config-forge:${rootProject.cloth_config_version}")
    modImplementation("vazkii.patchouli:Patchouli:${rootProject.patchouli_version}")

//    modImplementation("curse.maven:jei-238222:3295418")
//    modImplementation("curse.maven:xaero-263420:3298032")

    implementation(project(path: ":common")) {
        transitive = false
    }
    developmentForge(project(path: ":common")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) {
        transitive = false
    }

    annotationProcessor "org.spongepowered:mixin:0.8:processor"
}

processResources {
    inputs.property "version", project.version
    inputs.property "loader", "${rootProject.forge_loader_version}"

    filesMatching("META-INF/mods.toml") {
        expand "version": "${project.version}", "loader": "${rootProject.forge_loader_version}"
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "forge"
}

jar {
    classifier "dev"
}

java {
    withSourcesJar()
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

//sourceSets.main.resources {
//    srcDir 'src/generated/resources'
//}

curseforge {
    if (file("../key.properties").exists()) {
        def curseProp = new Properties()
        File secretPropsFile = file("../key.properties")
        curseProp.load(secretPropsFile.newInputStream())
        apiKey = curseProp.getProperty('curseKey')

        project {
            id = "415057"
            releaseType = "alpha"
            changelogType = 'text'
            changelog = file('../changelog.txt')
            addGameVersion("1.16.5")
            addGameVersion("Forge")
            relations {
                requiredDependency 'geckolib'
                requiredDependency 'architectury-forge'
                requiredDependency 'cloth-config-forge'
                optionalDependency 'patchouli'
            }
            mainArtifact(remapJar.archivePath) {
                displayName = "Outvoted-${mod_version}-forge"
            }
            afterEvaluate {
                uploadTask.dependsOn("build")
            }
        }
        options {
            forgeGradleIntegration = false
            javaVersionAutoDetect = false
        }
    }
}

import com.modrinth.minotaur.TaskModrinthUpload

task modrinthForge(type: TaskModrinthUpload) {
    if (file("../key.properties").exists()) {
        def modrinthProp = new Properties()
        File secretPropsFile = file("../key.properties")
        modrinthProp.load(secretPropsFile.newInputStream())
        token = modrinthProp.getProperty('modrinthKey')
        projectId = 'klAfKAj3'
        versionNumber = project.version + "-forge"
        uploadFile = remapJar.archivePath // This is the java jar task
        changelog = file('changelog.txt')
        releaseType = "alpha"
        addGameVersion('1.16.5')
        addLoader('forge')
    }
}