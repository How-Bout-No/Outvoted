plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

configurations {
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}

minecraft {
    accessWidener = file("src/main/resources/outvoted.accesswidener")
}

architectury {
    platformSetupLoomIde()
    fabric()
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "me.shedaniel:architectury-fabric:${rootProject.architectury_version}"

    modImplementation "software.bernie.geckolib:geckolib-fabric-1.16.5:${rootProject.geckolib_version}:dev"
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation "com.terraformersmc:modmenu:${rootProject.modmenu_version}"
    modImplementation("vazkii.patchouli:Patchouli:${rootProject.patchouli_version}-FABRIC") {
        exclude(group: "net.fabricmc.fabric-api")
    }

//    modImplementation("curse.maven:xaero-263420:3298031")
    modImplementation("curse.maven:toro-245733:3144149")

    implementation(project(path: ":common")) {
        transitive = false
    }
    developmentFabric(project(path: ":common")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "fabric"
}

jar {
    classifier "dev"
}

java {
    withSourcesJar()
}

//sourceSets.main.resources {
//    srcDir '../forge/src/generated/resources'
//}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

curseforge {
    if (file("../key.properties").exists()) {
        def curseProp = new Properties()
        File secretPropsFile = file("../key.properties")
        curseProp.load(secretPropsFile.newInputStream())
        apiKey = curseProp.getProperty('curseKey')

        project {
            id = "415057"
            releaseType = "alpha"
            changelogType = 'text'
            changelog = file('../changelog.txt')
            addGameVersion("1.16.5")
            addGameVersion("Fabric")
            relations {
                requiredDependency 'geckolib-fabric'
                requiredDependency 'architectury-fabric'
                requiredDependency 'cloth-config'
                requiredDependency 'fabric-api'
                optionalDependency 'patchouli-fabric'
                optionalDependency 'modmenu'
            }
            mainArtifact(remapJar.archivePath) {
                displayName = "Outvoted-${mod_version}-fabric"
            }
            afterEvaluate {
                uploadTask.dependsOn("build")
            }
        }
        options {
            forgeGradleIntegration = false
            javaVersionAutoDetect = false
        }
    }
}

import com.modrinth.minotaur.TaskModrinthUpload

task modrinthFabric(type: TaskModrinthUpload) {
    if (file("../key.properties").exists()) {
        def modrinthProp = new Properties()
        File secretPropsFile = file("../key.properties")
        modrinthProp.load(secretPropsFile.newInputStream())
        token = modrinthProp.getProperty('modrinthKey')
        projectId = 'klAfKAj3'
        versionNumber = project.version + "-fabric"
        versionName = "[FABRIC] " + project.version
        uploadFile = remapJar.archivePath // This is the java jar task
        changelog = file('../changelog.txt').absoluteFile
        versionType = "ALPHA"
        addGameVersion('1.16.5')
        addLoader('fabric')
    }
}