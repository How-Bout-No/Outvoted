plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "com.matthewprenger.cursegradle"
}

configurations {
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}

minecraft {
    accessWidener = file("src/main/resources/outvoted.accesswidener")
}

architectury {
    platformSetupLoomIde()
    fabric()
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "me.shedaniel:architectury-fabric:${rootProject.architectury_version}"

    modImplementation "software.bernie.geckolib:geckolib-fabric-1.16.5:${rootProject.geckolib_version}:dev"
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation "com.terraformersmc:modmenu:1.16.9"
    modImplementation("vazkii.patchouli:Patchouli:${rootProject.patchouli_version}-FABRIC")

    implementation(project(path: ":common")) {
        transitive = false
    }
    developmentFabric(project(path: ":common")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "fabric"
}

jar {
    classifier "dev"
}

java {
    withSourcesJar()
}

//sourceSets.main.resources {
//    srcDir '../forge/src/generated/resources'
//}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

import com.modrinth.minotaur.TaskModrinthUpload

if (file("key.properties").exists()) {
    curseforge {
        def curseProp = new Properties()
        curseProp.load(secretPropsFile.newInputStream())
        apiKey = curseProp.getProperty('curseKey')

        project {
            id = ""
            releaseType = "release"
            changelogType = 'text'
            changelog = file('changelog.txt')
            addGameVersion("1.16.5")
            addGameVersion("Fabric")

            mainArtifact(jar) {
                displayName = "Outvoted-${minecraft_version}-${mod_version}"
                relations {
                    requiredDependency 'geckolib'
                    requiredDependency 'architectury-fabric'
                    optionalDependency 'patchouli'
                    optionalDependency 'cloth-config'
                    optionalDependency 'modmenu'
                }
            }
        }
    }

    task modrinthFabric (type: TaskModrinthUpload) {
        def modrinthProp = new Properties()
        modrinthProp.load(secretPropsFile.newInputStream())
        token = modrinthProp.getProperty('modrinthKey')
        projectId = 'klAfKAj3'
        versionNumber = project.version + "-fabric"
        uploadFile = remapJar.archivePath // This is the java jar task
        changelog = file('changelog.txt')
        addGameVersion('1.16.5')
        addLoader('fabric')
    }
}